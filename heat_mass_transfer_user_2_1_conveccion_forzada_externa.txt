{TODO:
-contrastar codigo con las ecuaciones las tablas del resúmen del cap 7, Incropera 8ed}
$KeyBoard US
$UnitSystem SI K kPa kJ mass
$CHECKUNITS AutoOn
 
$BookMark Error Caller
Procedure error_interval(n,arrayVarValues[1..n],arrayVarNames$[1..n],arrayVarInterv$[1..n]:Error$)
{$error_interval
Genera un mesaje de error personalizado para ser usado junto con 
el procedimiento en Call Error($...,...) de la forma: "correlación 
no aplicable: fuera de intervalo; VarNames$; IntervalValues$". 
 
Finalmente, indica, mediante la ventada de advertencias (Ctrl+W), 
los valores de las respectivas variables en contraste con los 
valores de los intervalos de validez.
-----------------------------------------------------------------
Generates a custom error message to be used with the procedure in 
Call Error($...,...) in the format: "correlation not applicable: 
out of range; VarNames$; IntervalValues$."
 
Finally, indicate, using the warning window (Ctrl+W), the values 
of the respective variables in contrast with the valid interval 
ranges.}
	"base out of range message construction"
	errorMsg$:='correlation not valid: out of range; '||'actual values: ['
	errorMsgIntervals$:=''
Duplicate i=1,n	"logic check"
	warningMsg$:='value of '||arrayVarNames$[i]||': (XXXF1), '||'interval ('||arrayVarInterv$[i]||')'
	Call warning(warningMsg$,arrayVarValues[i])	"generate warning"
	errorMsg$:=errorMsg$||arrayVarNames$[i]||'='||string$(arrayVarValues[i])||' '	"showing actual values"
	errorMsgIntervals$:=errorMsgIntervals$||'{'||arrayVarInterv$[i]||'}'	"intervals"
End
	Error$:=errorMsg$||']'||errorMsgIntervals$	"errorMsg completed"
End
 
$BookMark Pi numbers/Números adimensionales
$BookMark Thermophysical props/Props termofísicas
$BookMark Thermophysical props/Props termofísicas: Film temp/Temp pelicula
Subprogram t_film(T_s,T_infinity:T_f)
{$T_FILM
Calcula la temperatura de pelicula, promedio, con base en la temperatura 
superficial y la temperatura del ambiente rodeando a la superficie.
 
->Todas las temperaturas deben ser suministradas en K
------------------------------------------------------------------------
Calculates the film temperature, averaged, based on the surface temperature 
and the temperature of the surrounding environment.
 
-> All temperatures must be provided in K.}
	$VarInfo T_s Units='K'	"superficial temp"
	$VarInfo T_infinity Units='K'	"enviromental temp"
	
	$VarInfo T_f Units='K'	"film temp"
	T_f=average(T_s,T_infinity)
End
$BookMark Pi numbers/Números Adimensionales: Reynolds
Subprogram reynolds_number(rho,V,Lc,mu: Re)
{$Reynolds_number
Calcula el número de Reynolds con base en la densidad del 
fluido, su velocidad, la longitud característica y la velocidad 
dinámica.
-Re=10000, flujo interno
-Re=20000, flujo externo
-Re=500000, flujo interno con obstaculo.
------------------------------------------------------------------------
Calculates the Reynolds number based on the fluid density, 
velocity, characteristic length, and dynamic viscosity.
-Re=10000, internal flow
-Re=20000, external flow
-Re=500000, internal flow with obstacle.}
    $VarInfo rho Units='kg/m^3'	"fluid density"
    $VarInfo V Units='m/s'	"fluid velocity"
    $VarInfo Lc Units='m'	"characteristic length"
    $VarInfo mu Units='kg/m-s'	"dynamic viscosity"
    
    $VarInfo Re Units='-'	"reynolds number"
    Re = rho * V * Lc / mu
End
$BookMark Pi numbers/Números adimensionales: Nusselt
$BookMark Pi numbers/Números adimensionales: Nusselt: Flat plate/Placa Plana
Subprogram nusselt_number_plane(h,L,k: Nu)
{$Nusselt_NUMBER_plane
Calcula el número de Nusselt con base en el coeficiente de convección, 
la distancia desde el borde de una superficie hasta el punto deseado y 
la conductividad del fluido.
------------------------------------------------------------------------
Calculates the Nusselt number based on the convection coef, 
the distance from the edge of the surface to the desired point 
and the fluid conductivity.}
    $VarInfo h Units='W/m^2-K'	"convection coeff"
    $VarInfo L Units='m'	"distance from edge"
    $VarInfo k Units='W/m-K'	"fluid conductivity"
    
	$VarInfo Nu Units='-'	"nusselt number"
    Nu = h*L/ k
End
 
$BookMark Forced Convection/Conveccion Forzada
$BookMark Forced Convection/Conveccion Forzada: External/Externa
$BookMark Forced Convection/Conveccion Forzada: External/Externa: Correlations/Correlaciones
Subprogram nusselt_number_corr(C,Re,m,Pr,n: Nu)
{$Nusselt_NUMBER_corr
Calcula el número de nusselt a partir de la correlación seleccionada, 
con base en los coeficientes C, m y n.
------------------------------------------------------------------------
Calculates the nusselt number from the selected correlation, 
bases on C,m and n coefficients.}
    $VarInfo C Units='-'	"C coeff"
    $VarInfo Re Units='-'	"Re number"
    $VarInfo m Units='-'	"m coeff"
    $VarInfo Pr Units='-'	"Pr number"
    $VarInfo n Units='-'	"n coeff"
    
    $VarInfo Nu Units='-'	"Nusselt number"
    Nu = C*Re^(m)*Pr^(n)	"correlation forms"
End
$BookMark Forced Convection/Conveccion Forzada: External/Externa: Correlations/Correlaciones: Flat plate/Placa plana
$BookMark Forced Convection/Conveccion Forzada: External/Externa: Correlations/Correlaciones: Flat plate/Placa plana: Local
Procedure nusselt_x(Re_x,Pr:Nu_x)
{$NUSSELT_X
Calcula el número de nusselt local, según si es flujo laminar 
o turbulento, para una placa plana, usando las correlaciones 
obtenidas en el cap 6, sec 6-10 del Cengel 6ed para transferencia.
 
Los valores no necesitan de unidades asignadas.
-----------------------------------------------------
Calculates the local Nusselt number, depending on whether the flow 
is laminar or turbulent, for a flat plate, using the correlations 
obtained in Chapter 6, Section 6-10 of Cengel 6th edition for heat 
transfer.
 
The values do not need assigned units.}
    $VarInfo Re_x Units='-'	"pi number Reynolds"
    $VarInfo Pr Units='-'	"pi number Prandtl"
	$VarInfo C Units='-'	"coeff C"
	$VarInfo m Units='-'	"coeff m"
	$VarInfo n Units='-'	"coeff n"
	
	$VarInfo Nu Units='-'	"pi number Nusselt"
    $VarInfo Nu_x Units='-'	"pi number Nusselt local"
	option$='Error'	"default case"
	If ( Pr>=0.6 ) AND ( (Re_x>=0 ) AND (Re_x<500000 ) ) Then	"laminar?, Pr>=0.6?"
	option$='Laminar'
	Endif
	If (Pr<60) AND (Re_x>=500000) Then	"turbulent?, Pr<60?"
	option$='Turbulento'	
	Endif
	
	Case option$
	'Laminar' :: C:=0.332
	m:=1/2
	n:=1/3
	Call nusselt_number_corr(C,Re_x,m,Pr,n:Nu)
	Nu_x:=Nu
	'Turbulento':: C:=0.0296
	m:=4/5
	n:=1/3
	Call nusselt_number_corr(C,Re_x,m,Pr,n:Nu)
	Nu_x:=Nu
	Else :: "case error"
	j=2	"amount of variables for the process"
	arrayVarValues[1..j]=[Re_x,Pr]	"vars current values"
	arrayVarNames$[1..j]=['Re_x','Pr']	"vars names"
	PrInterval$='0.6-infinity'	"laminar, Pr>=0.6?"
	If (Re_x>5*10^5) Then
	PrInterval$='0.6-60'	"turbulent, Pr<60?"
	Endif
	arrayVarInterv$[1..j]=['0-infinity',PrInterval$]	"vars valid intervals"
	Call error_interval(j,arrayVarValues[1..j],arrayVarNames$[1..j],arrayVarInterv$[1..j]:Err$)
	Call error(Err$)	"outside interval error"
	Endcase
End
 
$BookMark Conveccion Forzada: Externa: Correlaciones: Placa plana: Promedio
Procedure nusselt_avg(Re_L,Pr:Nu_avg)
{$NUSSELT_avg
Calcula el número de nusselt promedio, según si es flujo laminar 
o turbulento, para una placa plana.
 
->Los valores no necesitan de unidades asignadas.
----------------------------------------------------------------
Calculates the average Nusselt number, depending on whether the flow
 is laminar or turbulent, for a flat plate.
 
-> The values do not need assigned units.}
    $VarInfo Re_L Units='-'	"pi number Reynolds"
    $VarInfo Pr Units='-'	"pi number Prandtl"
	$VarInfo C Units='-'	"coeff C"
	$VarInfo m Units='-'	"coeff m"
	$VarInfo n Units='-'	"coeff n"
	
	$VarInfo Nu Units='-'	"pi number Nusselt"
    $VarInfo Nu_avg Units='-'	"pi number Nusselt avg"
	option$='Error'
	If (Pr>=0.6) AND ( (Re_L>=0) AND (Re_L<500000 ) ) Then	"laminar flow?, Pr>=0.6?"
	option$='Laminar'
	Endif
	If (Pr<60) AND ( (Re_L>=500000) AND (Re_L<=999*10^7) ) Then	"turbulento flow?, Pr<60?"
	option$='Turbulento'	
	Endif
	
	Case option$
	'Laminar':: C:=0.664
	m:=1/2
	n:=1/3
	Call nusselt_number_corr(C,Re_L,m,Pr,n:Nu)
	Nu_avg:=Nu
	'Turbulento':: C:=0.037
	m:=4/5
	n:=1/3
	Call nusselt_number_corr(C,Re_L,m,Pr,n:Nu)
	Nu_avg:=Nu
	Else:: "caso error"
	j=2	"amount of input variables for the process"
	arrayVarValues[1..j]=[Re_L,Pr]	"vars current values"
	arrayVarNames$[1..j]=['Re_L','Pr']	"error potential vars"
	PrInterval$='0.6-infinity'	"laminar flow, Pr>=0.6?"
	ReTurbulentInterval$='0-infinity'	"laminar flow"
	If (Re_x>5*10^5) Then
	PrInterval$='0.6-60'	"turbulent flow, Pr<=60?"
	ReTurbulentInterval$='0-999*10^7'
	Endif
	arrayVarInterv$[1..j]=[ReTurbulentInterval$,PrInterval$]	"error potential intervals"
	Call error_interval(j,arrayVarValues[1..j],arrayVarNames$[1..j],arrayVarInterv$[1..j]:Err$)
	Call error(Err$)	"outside of interval"
	Endcase
End
 
$BookMark Forced Convection/Conveccion Forzada: External/Externa: Correlations/Correlaciones: Flat plate/Placa plana: Temp constant long/Longitud a temp cte
Procedure nusselt_x_leq(Re_x,Pr,x,xi:Nu_x)
{$NUSSELT_X_LEQ
Calcula el número de nusselt local, según si es flujo laminar o turbulento, para una placa plana 
con una sección no calentada o en equilibrio térmico con el ambiente}
    $VarInfo Re_x Units='-'	"pi number Reynolds"
    $VarInfo Pr Units='-'	"pi number Prandtl"
	$VarInfo x Units='m'	"distance calculation"
	$VarInfo xi Units='m'	"equilibrium distance"
 	
	$VarInfo Nu Units='-'	"pi number Nusselt"
    $VarInfo Nu_x Units='-'	"pi number Nusselt local"
	option$='Error'
	If (x>=xi) Then	"equilibrium long>distance calculation?"
	If (Re_x<500000) Then	"laminar flow?"
	option$='Laminar'
	Endif
	If (Re_x>=5*10^(5)) Then	"turbulent flow?"
	option$='Turbulento'
	Endif
	Endif
	
	Case option$
	'Laminar'::Call nusselt_x(Re_x,Pr:Nu)	"call nusselt local, laminar plate"
	Nu_x:=Nu/( 1-(xi/x)^(3/4) )^(1/3)
	'Turbulento'::Call nusselt_x(Re_x,Pr:Nu)	"call nusselt local, turbulent plate"
	Nu_x:=Nu/( 1-(xi/x)^(9/10) )^(1/9)	
	Else:: "caso error"
	j=1	"amount of input variables for the process"
	arrayVarValues[1..j]=[Re_x]	"vars current values"
	arrayVarNames$[1..j]=['Re_x']	"error potential vars"
	ReTurbulentInterval$='0-infinity'	"laminar flow"
	arrayVarInterv$[1..j]=[ReTurbulentInterval$]	"error potential intervals"
	Call error_interval(j,arrayVarValues[1..j],arrayVarNames$[1..j],arrayVarInterv$[1..j]:Err$)
	Call error(Err$)	"outside of interval"
	Endcase
End
 
$BookMark Forced Convection/Conveccion Forzada: External/Externa: Correlations/Correlaciones: Flat plate/Placa plana: Uniform Superficial Heat/Calor superficial uniforme
Procedure nusselt_x_q_uniform(Re_x,Pr:Nu_x)
{$NUSSELT_X_q_uniform
Calcula el número de nusselt local, según si es flujo laminar o 
turbulento, para una placa plana, en condición de calor 
superficial uniforme
------------------------------------------------------------------------
Calculate the local Nusselt number, depending on whether the flow 
is laminar or turbulent, for a flat plate under uniform surface 
heat conditions.}
	$VarInfo Re_x Units='-'	"pi number Reynolds"
    $VarInfo Pr Units='-'	"pi number Prandtl"
	$VarInfo C Units='-'	"coeff C"
	$VarInfo m Units='-'	"coeff m"
	$VarInfo n Units='-'	"coeff n"
	
	$VarInfo Nu Units='-'	"pi number Nusselt"
    $VarInfo Nu_x Units='-'	"pi number Nusselt local"
	option$='Error'	"default case"
	If (Pr>=0.6) AND ( (Re_x>=0) AND (Re_x<500000 ) ) Then	"laminar?, Pr>=0.6?"
	option$='Laminar'
	Endif
	If (Pr<60) AND (Re_x>=500000) Then	"turbulent?, Pr<60?"
	option$='Turbulento'	
	Endif
	
	Case option$
	'Laminar':: C:=0.453
	m:=1/2
	n:=1/3
	Call nusselt_number_corr(C,Re_x,m,Pr,n:Nu)
	Nu_x:=Nu
	'Turbulento':: C:=0.0308
	m:=4/5
	n:=1/3
	Call nusselt_number_corr(C,Re_x,m,Pr,n:Nu)
	Nu_x:=Nu
	Else:: "case error"
	j=2	"amount of variables for the process"
	arrayVarValues[1..j]=[Re_x,Pr]	"vars current values"
	arrayVarNames$[1..j]=['Re_x','Pr']	"vars names"
	ReInterval$='0-infinity'
	PrInterval$='0.6-infinity'	"laminar, Pr>=0.6?"
	If (Re_x>5*10^5) Then
	PrInterval$='0.6-60'	"turbulent, Pr<60?"
	Endif
	arrayVarInterv$[1..j]=[ReInterval$,PrInterval$]	"vars valid intervals"
	Call error_interval(j,arrayVarValues[1..j],arrayVarNames$[1..j],arrayVarInterv$[1..j]:Err$)
	Call error(Err$)	"outside interval error"
	Endcase
End
 
$BookMark Forced Convection/Conveccion Forzada: External/Externa: Correlations/Correlaciones: Crossflow/Flujo cruzado
$BookMark Forced Convection/Conveccion Forzada: External/Externa: Correlations/Correlaciones: Crossflow/Flujo cruzado: Circle/Circulo
Procedure nusselt_circulo(Re, Pr: Nu_avg)
{$Nusselt_Circulo
Calcula el número de Nusselt promedio para cilindros con 
sección transversal circular en flujo cruzado. Aplica para 
gas o líquido.
----------------------------------------------------------
Calculate the average Nusselt number for cylinders with a 
circular cross-section in cross-flow. Applicable for gas 
or liquid.}
    $VarInfo Re Units='-' "Número de Reynolds"
    $VarInfo Pr Units='-' "Número de Prandtl"
	$VarInfo C Units='-'	"coeficiente C"
	$VarInfo m Units='-'	"coeficiente m"
	$VarInfo n Units='-'	"coeficiente n"
	
	$VarInfo Nu Units='-'	"pi number Nusselt"
    $VarInfo Nu_avg Units='-' "Número de Nusselt promedio"
	option$='Error'	"default case"
    n:=1/3
	ReInterval$='0.4-400000'	"valid interval Re"
	If (Re >= 0.4) AND (Re < 4) Then
	option$='1'
	Endif
    If (Re >= 4) AND (Re < 40) Then
	option$='2'
	Endif
    If (Re >= 40) AND (Re < 4000) Then
	option$='3'
	Endif
    If (Re >= 4000) AND (Re < 40000) Then
	option$='4'
	Endif
    If (Re >= 40000) AND (Re < 400000) Then
	option$='5'
	Endif
	
	Case option$
	'1':: C:=0.989
	m:=0.33
	Call nusselt_number_corr(C,Re,m,Pr,n:Nu)
	Nu_avg :=Nu
	'2':: C:=0.911
	m:=0.385
	Call nusselt_number_corr(C,Re,m,Pr,n:Nu)
	Nu_avg := Nu
	'3':: C:=0.683
	m:=0.466
	Call nusselt_number_corr(C,Re,m,Pr,n:Nu)
	Nu_avg := Nu
	'4':: C:=0.193
	m:=0.618
	Call nusselt_number_corr(C,Re,m,Pr,n:Nu)
	Nu_avg := Nu
	'5':: C:=0.027
	m:=0.805
	Call nusselt_number_corr(C,Re,m,Pr,n:Nu)
	Nu_avg := Nu
	Else:: "case error"
	j=2	"amount of variables for the process"
	arrayVarValues[1..j]=[Re,Pr]	"vars current values"
	arrayVarNames$[1..j]=['Re','Pr']	"vars names"
	PrInterval$='0-infinity'	"laminar, Pr>=0.6?"
	arrayVarInterv$[1..j]=[ReInterval$,PrInterval$]	"vars valid intervals"
	Call error_interval(j,arrayVarValues[1..j],arrayVarNames$[1..j],arrayVarInterv$[1..j]:Err$)
	Call error(Err$)	"outside interval error"
	Endcase
End
 
$BookMark Forced Convection/Conveccion Forzada: External/Externa: Correlations/Correlaciones: Crossflow/Flujo cruzado: Square/Cuadrado
Procedure nusselt_cuadrado(angle,Re, Pr: Nu_avg)
{$Nusselt_Cuadrado
Calcula el número de Nusselt promedio para cilindros con áre 
transversal cuadrada en flujo cruzado.
--->Aplica solo para gases.
-------------------------------------------------------
Calculates the average nusselt number for a cylinder with a 
squared cross secction on cross-flow.
--->Only applies for gas flows}
	$VarInfo angle Units='-' "angle"
    $VarInfo Re Units='-' "Reynolds"
    $VarInfo Pr Units='-' "Prandtl"
	$VarInfo C Units='-'	"coeff C"
	$VarInfo m Units='-'	"coeff m"
	$VarInfo n Units='-'	"coeff n"
	
	$VarInfo Nu Units='-'	"pi number Nusselt"
    $VarInfo Nu_avg Units='-' "Nusselt avg"
	option$='Error'	"default case"
	ReInterval$='3900-1.6^(6)'	"valid interval Re"
	n:=1/3
	If (Re >= 3900) AND (Re <= 2.3*10^(6)) AND (angle>=0) Then	"all valid"
	If (angle=45) Then
	ReInterval$='3900-2.3^(6)'	"valid interval Re"
	If (Re>=5.6*10^3) AND (Re<=46*10^3) Then
	option$='3'
	Else
	option$='4'
	Endif
	Endif
	If (Re <=32*10^3 ) Then
	option$='1'
    Endif
	If (Re >32*10^3 ) AND (Re <=1.6*10^6 ) Then
	option$='2'
    Endif
	Endif
	
	Case option$
	'1'::C:= 0.094
	m:=0.675
	Call nusselt_number_corr(C,Re_x,m,Pr,n:Nu)
	Nu_avg := Nu
	'2'::C:= 0.0249
	m:=0.811
	Call nusselt_number_corr(C,Re_x,m,Pr,n:Nu)
	Nu_avg := Nu
	'3'::C:=  0.258
	m:=0.588
	Call nusselt_number_corr(C,Re_x,m,Pr,n:Nu)
	Nu_avg := Nu
	'4'::C:=  0.0260
	m:=0.839
	Call nusselt_number_corr(C,Re_x,m,Pr,n:Nu)
	Nu_avg := Nu
	Else:: "case error"
	j=3	"amount of variables for the process"
	arrayVarValues[1..j]=[angle,Re,Pr]	"vars current values"
	arrayVarNames$[1..j]=['angle','Re','Pr']	"vars names"
	angleInterval$='0°,45°'	"valid interval angle"
	PrInterval$='0-infinity'	"valid interval Pr"
	arrayVarInterv$[1..j]=[angleInterval$,ReInterval$,PrInterval$]	"vars valid intervals"
	Call error_interval(j,arrayVarValues[1..j],arrayVarNames$[1..j],arrayVarInterv$[1..j]:Err$)
	Call error(Err$)	"outside interval error"
	Endcase
End
 
$BookMark Forced Convection/Conveccion Forzada: External/Externa: Correlations/Correlaciones: Crossflow/Flujo cruzado: Hexagone/Hexágono
Procedure nusselt_hexagono(angle,Re, Pr: Nu_avg)
{$Nusselt_Hexagono
Calcula el número de Nusselt promedio para cilindros con área 
transversal hexagonal en flujo cruzado.
--->Aplica solo para gases.
-------------------------------------------------------
Calculates the average nusselt number for a cylinder with a 
hexagonal cross secction on cross-flow.
--->Only applies for gas flows}
	$VarInfo angle Units='-' "angle"
    $VarInfo Re Units='-' "Reynolds"
    $VarInfo Pr Units='-' "Prandtl"
	$VarInfo C Units='-'	"coeff C"
	$VarInfo m Units='-'	"coeff m"
	$VarInfo n Units='-'	"coeff n"
	
	$VarInfo Nu Units='-'	"pi number Nusselt"
    $VarInfo Nu_avg Units='-' "Nusselt avg"
	option$='Error'	"default case"
	ReInterval$='4.5*10^3-90.7*10^3'	"valid interval Re"
	n:=1/3
	If (Re >= 4500) AND (Re <= 105*10^(3)) AND (angle>=0) Then	"all valid"
	If (angle=45) Then
	ReInterval$='5.2*10^3-105*10^3'	"valid interval Re"
	If (Re>=5.2*10^3) AND (Re<20.4*10^3) Then
	option$='2'
	Else
	option$='3'
	Endif
	Endif
	If (Re <=90.7*10^3 ) Then
	option$='1'
    Endif
	Endif
	
	Case option$
	'1'::C:= 0.148
	m:=0.638
	Call nusselt_number_corr(C,Re_x,m,Pr,n:Nu)
	Nu_avg := Nu
	'2'::C:= 0.162
	m:=0.638
	Call nusselt_number_corr(C,Re_x,m,Pr,n:Nu)
	Nu_avg := Nu
	'3'::C:= 0.039
	m:=0.782
	Call nusselt_number_corr(C,Re_x,m,Pr,n:Nu)
	Nu_avg := Nu
	Else:: "case error"
	j=3	"amount of variables for the process"
	arrayVarValues[1..j]=[angle,Re,Pr]	"vars current values"
	arrayVarNames$[1..j]=['angle','Re','Pr']	"vars names"
	angleInterval$='0°,45°'	"valid interval angle"
	PrInterval$='0-infinity'	"valid interval Pr"
	arrayVarInterv$[1..j]=[angleInterval$,ReInterval$,PrInterval$]	"vars valid intervals"
	Call error_interval(j,arrayVarValues[1..j],arrayVarNames$[1..j],arrayVarInterv$[1..j]:Err$)
	Call error(Err$)	"outside interval error"
	Endcase
End
 
$BookMark Forced Convection/Conveccion Forzada: External/Externa: Correlations/Correlaciones: Crossflow/Flujo cruzado: Vertical plate/Placa vertical
Procedure nusselt_placa_vertical(Re, Pr: Nu_avg)
{$nusselt_placa_vertical
Calcula el número de Nusselt promedio para una placa vertical 
(90° respecto a la dirección del flujo) en flujo cruzado.
--->Aplica solo para gases.
-------------------------------------------------------
Calculates the average nusselt number for a vertical plate 
(90° with respect the flow direction) on cross-flow.
--->Only applies for gas flows}
	$VarInfo angle Units='-' "angle"
    $VarInfo Re Units='-' "Reynolds"
    $VarInfo Pr Units='-' "Prandtl"
	$VarInfo C Units='-'	"coeff C"
	$VarInfo m Units='-'	"coeff m"
	$VarInfo n Units='-'	"coeff n"
	
	$VarInfo Nu Units='-'	"pi number Nusselt"
    $VarInfo Nu_avg Units='-' "Nusselt avg"
	option$='Error'	"default case"
	ReInterval$='6.3*10^3-23.6*10^3'	"valid interval Re"
	If (Re >= 6.3*10^3) AND (Re <= 23.6*10^3) AND (Pr>=0) Then	"all valid"
	option$='1'
	Endif
	
	Case option$
	'1'::C:= 0.257
	m:=0.731
	n:=1/3
	Call nusselt_number_corr(C,Re_x,m,Pr,n:Nu)
	Nu_avg := Nu
	Else:: "case error"
	j=2	"amount of variables for the process"
	arrayVarValues[1..j]=[Re,Pr]	"vars current values"
	arrayVarNames$[1..j]=['Re','Pr']	"vars names"
	PrInterval$='0-infinity'	"valid interval Pr"
	arrayVarInterv$[1..j]=[ReInterval$,PrInterval$]	"vars valid intervals"
	Call error_interval(j,arrayVarValues[1..j],arrayVarNames$[1..j],arrayVarInterv$[1..j]:Err$)
	Call error(Err$)	"outside interval error"
	Endcase
End
 
$BookMark Forced Convection/Conveccion Forzada: External/Externa: Correlations/Correlaciones: Crossflow/Flujo cruzado: Ellipse/Elipse
Procedure nusselt_elipse(Re, Pr: Nu_avg)
{$nusselt_elipse
Calcula el número de Nusselt promedio para cilindros con área 
transversal eliptica en flujo cruzado.
--->Aplica solo para gases.
-------------------------------------------------------
Calculates the average nusselt number for a cylinder with a 
eliptical cross secction on cross-flow.
--->Only applies for gas flows}
	$VarInfo angle Units='-' "angle"
    $VarInfo Re Units='-' "Reynolds"
    $VarInfo Pr Units='-' "Prandtl"
	$VarInfo C Units='-'	"coeff C"
	$VarInfo m Units='-'	"coeff m"
	$VarInfo n Units='-'	"coeff n"
	
	$VarInfo Nu Units='-'	"pi number Nusselt"
    $VarInfo Nu_avg Units='-' "Nusselt avg"
	option$='Error'	"default case"
	ReInterval$='1.4*10^3-8.2*10^3'	"valid interval Re"
	If (Re >= 1.4*10^3) AND (Re <= 8.2*10^3) AND (Pr>=0) Then	"all valid"
	option$='1'
	Endif
	
	Case option$
	'1'::C:= 0.197
	m:=0.612
	n:=1/3
	Call nusselt_number_corr(C,Re_x,m,Pr,n:Nu)
	Nu_avg := Nu
	Else:: "case error"
	j=2	"amount of variables for the process"
	arrayVarValues[1..j]=[Re,Pr]	"vars current values"
	arrayVarNames$[1..j]=['Re','Pr']	"vars names"
	PrInterval$='0-infinity'	"valid interval Pr"
	arrayVarInterv$[1..j]=[ReInterval$,PrInterval$]	"vars valid intervals"
	Call error_interval(j,arrayVarValues[1..j],arrayVarNames$[1..j],arrayVarInterv$[1..j]:Err$)
	Call error(Err$)	"outside interval error"
	Endcase
End

$BookMark Forced Convection/Conveccion Forzada: External/Externa: Correlations/Correlaciones: Crossflow/Flujo cruzado: Cylinder/Cilindro
Procedure nusselt_cilindro(Re, Pr: Nu_cil)
{$Nusselt_Cilindro
Calcula el número de Nusselt para un cilindro en flujo.
Esta correlación aplica para valores de Re Pr > 0.2; mayor 
exactitud.
------------------------------------------------------------
Calculates the Nusselt number for a cylinder through a flow.
This correlation is valid for Re Pr > 0.2. More accuracy}
 
    $VarInfo Re Units='-' "Reynolds number"
    $VarInfo Pr Units='-' "Prandtl number"
    $VarInfo Nu_cil Units='-' "Nusselt number for cylinder"
 
    // Verificación del intervalo de validez de la correlación
    If (Re * Pr <= 0.2) Then
    Call error('Correlación no válida: fuera de intervalo; Re Pr > 0.2')
    Else
    Nu_cil := 0.3 + (0.62 * Re^(1/2) * Pr^(1/3)) / ((1 + (0.4 / Pr)^(2/3))^(1/4)) * (1 + (Re / 282000)^(5/8))^(4/5)
    Endif
End

$BookMark Forced Convection/Conveccion Forzada: External/Externa: Correlations/Correlaciones: Crossflow/Flujo cruzado: Sphere/Esfera
Procedure nusselt_esfera(Re, Pr, mu_inf, mu_s: Nu_esf)
{$Nusselt_Esfera
Calcula el número de Nusselt para una esfera en flujo cruzado utilizando la correlación de Whitaker.
Esta correlación es válida para 3.5 ≤ Re ≤ 8 x 10^4, 0.7 ≤ Pr ≤ 380 y 1.0 ≤ (mu_inf / mu_s) ≤ 3.2.
------------------------------------------------------------------------
Calculates the Nusselt number for a sphere in crossflow using Whitaker's correlation.
This correlation is valid for 3.5 ≤ Re ≤ 8 x 10^4, 0.7 ≤ Pr ≤ 380, and 1.0 ≤ (mu_inf / mu_s) ≤ 3.2.}

$VarInfo Re Units='-' 'Reynolds number'
$VarInfo Pr Units='-' 'Prandtl number'
$VarInfo mu_inf Units='kg/m-s' 'Dynamic viscosity at free stream temperature'
$VarInfo mu_s Units='kg/m-s' 'Dynamic viscosity at surface temperature'
$VarInfo Nu_esf Units='-' 'Nusselt number for sphere'

// Verificación del intervalo de validez de la correlación
If (Re >= 3.5) AND (Re <= 80000) AND (Pr >= 0.7) AND (Pr <= 380) AND ((mu_inf / mu_s) >= 1.0) AND ((mu_inf / mu_s) <= 3.2) Then
Nu_esf := 2 + (0.4 * Re^(1/2) + 0.06 * Re^(2/3)) * Pr^(0.4) * (mu_inf / mu_s)^(1/4)
Else
Call error('Correlación no válida: fuera de intervalo; 3.5 ≤ Re ≤ 8x10^4, 0.7 ≤ Pr ≤ 380, 1.0 ≤ mu_inf/mu_s ≤ 3.2')
EndIf
End


$BookMark Forced Convection/Conveccion Forzada: External/Externa: Correlations/Correlaciones: Crossflow/Flujo cruzado:Tube banks/Banco de tubos
Procedure nusselt_tubebanks_D(arrgmt$,N_L,S_L,S_T,Re_D,Pr,Pr_s : Nu_avg)
{$nusselt_tubebanks_D
Calcula el número de Nusselt para un banco de tubos, alineados o 
escalonados (arrgmt$='l' or 's'), de acuerdo con el intervalo de 
validez correspondiente para el Reynolds y el Prandtl. Para el 
valor de $\xi$, se requiere de S_L y S_T; para el $f_f$, se requiere 
del Re_Dmax. Todas las propiedades termofísicas deben ser evaluadas 
@ con el promedio entre las temperaturas de entrada y de salida del 
banco, salvo por Pr_s, que debe evaluarse @ la temperatura de pelicula.
--------------------------------------------------------------
Calculate the Nusselt number for a tube bank, either aligned or 
staggered (arrgmt$='l' or 's'), according to the corresponding validity 
range for Reynolds and Prandtl numbers. For the value of $\xi$, you will 
need S_L​ and S_T; for $\f_f$​, the Reynolds number Re_Dmax​ is required. 
All thermophysical properties should be evaluated based on the average 
between the inlet and outlet temperatures of the bank, except for Pr_s​, 
which should be evaluated at the film temperature.
}
	$VarInfo Re_D Units='-'	"reynolds"
	$VarInfo Re_Dmax Units='-'	"reynolds max"
	$VarInfo Pr Units='-'	"Prandtl"
	$VarInfo Pr_s Units='-'	"Prandtl @ T_film"
	$VarInfo N_L Units='-'	"number of tubes on a row"
	$VarInfo S_L Units='m'	"S_L: longitudinal pitch"
	$VarInfo S_T Units='m'	"S_T: transversal pitch"
	$VarInfo C Units='-'	"coeff C"
	$VarInfo m Units='-'	"coeff m"
	$VarInfo n Units='-'	"coeff n"
	$VarInfo o Units='-'	"coeff o"
	
	$VarInfo Nu Units='-'	"pi number Nusselt"
	$VarInfo Nu_avg Units='-'	"nusselt_avg"
	option$='Error'
	NLValidInterval$='1-5,7,10,13,16-infinity'	"amount of tubes per row"
	ReValidInterval$='0-2*10^6'
	F:=1	"default value correction factor"
	If (N_L<16) Then	"number of tubes per row<16?"
	NLValidInterval$='1-5,7,10,13'	"amount of tubes per row"
	ReValidInterval$='1000-2*10^6'	"Reynolds valid interval for this case"
	k=8	"correction factor construction"
	CorrF[1..k,1]=[0.7,0.8,0.86,0.9,0.93,0.96,0.98,0.99]
	CorrF[1..k,2]=[0.64,0.76,0.84,0.89,0.93,0.96,0.98,0.99]
	row:=1	"default aligned"
	If (arrgmt$='s') Then
	row:=2	"selected row for correction factor will be staggered"
	Endif
	Case N_L
	1:: F:=CorrF[1,row]
	2:: F:=CorrF[1,row]
	3:: F:=CorrF[1,row]
	4:: F:=CorrF[1,row]
	5:: F:=CorrF[1,row]
	7:: F:=CorrF[1,row]
	10:: F:=CorrF[1,row]
	13:: F:=CorrF[1,row]
	Else:: "not valid tube amount"
	j=1	"amount of variables for the process"
	arrayVarValues[1..j]=[N_L]	"vars current values"
	arrayVarNames$[1..j]=['N_L']	"vars names"
	arrayVarInterv$[1..j]=[NLValidInterval$]	"vars valid intervals"
	Call error_interval(j,arrayVarValues[1..j],arrayVarNames$[1..j],arrayVarInterv$[1..j] : Err$)
	Call error(Err$)	"outside interval error"
	EndCase
	Endif
	
	p= 0.2	"power coeff to relation S_T/S_L"
	o= 0.25	"power coeff to relation Pr/Pr_s"
	m:= 0.4	"most common n value"
	n:= 0.36	"most common n value"
	If ((Re_D>0) AND (Re_D<2*10^6)) AND ((Pr>0.7) AND (Pr<500)) AND ((Pr_s>0.7) AND (Pr_s<500)) Then	"validation"
	If (arrgmt$='l') Then	"aligned"
	If (Re_D<100) Then
	option$='1'
	Endif
	If (Re_D>=100) AND (Re_D<1*10^3) Then
	option$='2'
	Endif
	If (Re_D>=1*10^3) AND (Re_D<2*10^5) Then
	option$='3'
	Endif
	If (Re_D>=2*10^5) Then
	option$='4'
	Endif
	Else	"staggered"
	If (Re_D<500) Then
	option$='5'
	Endif
	If (Re_D>=500) AND (Re_D<1*10^3) Then
	option$='6'
	Endif
	If (Re_D>=1*10^3) AND (Re_D<2*10^5) Then
	option$='7'
	Endif
	If (Re_D>=2*10^5) Then
	option$='8'
	Endif
	Endif
	Endif
	
	Case option$
	'1' :: C:= 0.9
	Call nusselt_number_corr(C,Re_D,m,Pr,n : Nu)
	Nu_avg := F*Nu*(Pr/Pr_s)^o
	'2' :: C:=  0.52
	m:=0.5
	Call nusselt_number_corr(C,Re_D,m,Pr,n : Nu)
	Nu_avg := F*Nu*(Pr/Pr_s)^o
	'3' :: C:= 0.27
	m:= 0.63
	Call nusselt_number_corr(C,Re_D,m,Pr,n : Nu)
	Nu_avg := F*Nu*(Pr/Pr_s)^o
	'4' :: C:= 0.333
	m:= 0.8
	n:= 0.4
	Call nusselt_number_corr(C,Re_D,m,Pr,n : Nu)
	Nu_avg := F*Nu*(Pr/Pr_s)^o
	'5' :: C:= 1.04	"staggered cases"
	Call nusselt_number_corr(C,Re_D,m,Pr,n : Nu)
	Nu_avg := Nu*(Pr/Pr_s)^o
	'6' :: C:= 0.71
	m:= 0.5
	Call nusselt_number_corr(C,Re_D,m,Pr,n : Nu)
	Nu_avg := F*Nu*(Pr/Pr_s)^o
	'7' :: C:= 0.35
	m:= 0.6
	Call nusselt_number_corr(C,Re_D,m,Pr,n : Nu)
	Nu_avg := F*(S_T/S_L)^(p)*Nu*(Pr/Pr_s)^o
	'8' :: C:= 0.031
	m:= 0.8
	Call nusselt_number_corr(C,Re_D,m,Pr,n : Nu)
	Nu_avg := F*(S_T/S_L)^(p)*Nu*(Pr/Pr_s)^o
	Else ::	"case Error"
	j=4	"amount of variables for the process"
	arrayVarValues[1..j]=[N_L,Re_D,Pr,Pr_s]	"vars current values"
	arrayVarNames$[1..j]=['N_L','Re_D','Pr','Pr_s']	"vars names"
	PrValidInterval$='0.7-500'
	arrayVarInterv$[1..j]=[NLValidInterval$,ReValidInterval$,PrValidInterval$,PrValidInterval$]	"vars valid intervals"
	Call error_interval(j,arrayVarValues[1..j],arrayVarNames$[1..j],arrayVarInterv$[1..j] : Err$)
	Call error(Err$)	"outside interval error"
	EndCase
End

$BookMark forced convection/conveccion forzada: external/externa: crossflow/flujo cruzado: tube banks/banco de tubos: log mean temperature difference/diferencia de temperatura media logarítmica
Procedure log_mean_temp_diff(T_s, T_e, T_i: delta_Tlm)
{$log_mean_temp_diff
Calcula la diferencia de temperatura media logarítmica (LMTD) para un flujo en banco de tubos.
------------------------------------------------------------------------
Calculates the log mean temperature difference (LMTD) for tube bank flow.}

$VarInfo T_s Units='K' 'surface temperature'
$VarInfo T_e Units='K' 'exit temperature'
$VarInfo T_i Units='K' 'inlet temperature'
$VarInfo delta_Tlm Units='K' 'log mean temperature difference'

delta_Te := T_s - T_e
delta_Ti := T_s - T_i

// Verificación para evitar el logaritmo de 0 o división por 0
If delta_Te = delta_Ti Then
Call error('Error: delta_Te y delta_Ti son iguales, lo que lleva a una división por 0 en el cálculo de LMTD.')
Else
delta_Tlm := (delta_Te - delta_Ti) / ln(delta_Te / delta_Ti)
EndIf
End

$BookMark forced convection/conveccion forzada: external/externa: crossflow/flujo cruzado: tube banks/banco de tubos: exit temperature/temperatura de salida
Subprogram exit_temperature(T_s, T_i, A_s, h, m_dot, c_p: T_e)
{$exit_temperature
Calcula la temperatura de salida del fluido en un banco de tubos en flujo cruzado.
------------------------------------------------------------------------
Calculates the exit temperature of the fluid in a tube bank in crossflow.}

$VarInfo T_s Units='K' 'surface temperature'
$VarInfo T_i Units='K' 'inlet temperature'
$VarInfo A_s Units='m^2' 'heat transfer surface area'
$VarInfo h Units='W/m^2-K' 'heat transfer coefficient'
$VarInfo m_dot Units='kg/s' 'mass flow rate'
$VarInfo c_p Units='J/kg-K' 'specific heat capacity'
$VarInfo T_e Units='K' 'exit temperature'

T_e = T_s - (T_s - T_i) * exp(- (A_s * h) / (m_dot * c_p))
End

$BookMark forced convection/conveccion forzada: external/externa: crossflow/flujo cruzado: tube banks/banco de tubos: pressure drop/caída de presión
Subprogram pressure_drop(N_L, f, chi, rho, V_max: delta_P)
{$pressure_drop
Calcula la caída de presión (?P) en un banco de tubos en flujo cruzado.
------------------------------------------------------------------------
Calculates the pressure drop (?P) in a tube bank in crossflow.}
 
$VarInfo N_L Units='-' 'number of rows in the flow direction'
$VarInfo f Units='-' 'friction factor'
$VarInfo chi Units='-' 'correction factor'
$VarInfo rho Units='kg/m^3' 'fluid density'
$VarInfo V_max Units='m/s' 'maximum fluid velocity'
$VarInfo delta_P Units='Pa' 'pressure drop'
 
delta_P = N_L * f * chi * (rho * V_max^2) / 2
End

$BookMark forced convection/conveccion forzada: external/externa: crossflow/flujo cruzado: tube banks/banco de tubos: pumping power/potencia de bombeo
Subprogram pumping_power(m_dot, delta_P, rho: W_pump)
{$pumping_power
Calcula la potencia de bombeo (W_pump) requerida para superar la resistencia al flujo en un banco de tubos en flujo cruzado.
------------------------------------------------------------------------
Calculates the pumping power (W_pump) required to overcome flow resistance in a tube bank in crossflow.}
 
$VarInfo m_dot Units='kg/s' 'mass flow rate'
$VarInfo delta_P Units='Pa' 'pressure drop'
$VarInfo rho Units='kg/m^3' 'fluid density'
$VarInfo W_pump Units='W' 'pumping power'
 
W_pump = (m_dot * delta_P) / rho
End